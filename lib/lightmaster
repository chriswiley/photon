namespace eval ::Lightmaster {

proc init {} {
  set m [ menu .menubar.lightmaster -tearoff 0 ]
  .menubar add cascade -label Lightmaster -menu $m
  $m add command -label "Send Reset State" \
      -command [ namespace code "reset_state" ]
  $m add command -label "Run Idle Scene" \
      -command [ namespace code "start_idle" ]
  set trackmenu [ menu ${m}.track -tearoff 0 ]
  $m add cascade -label "Run Game Scene" -menu $trackmenu
  for { set t 1 } { $t <= 8 } { incr t } {
    $trackmenu add command -label "Track $t" \
      -command [ namespace code "start_game $t" ]
  }
}

proc reset_state {} {
  set reset_channel 10
  toggle_channel $reset_channel
}

proc start_idle {} {
  ::warninglog "Lightmaster -> IDLE"
  set idle_channel 9
  reset_state
  after 500 [ list ::Lightmaster::toggle_channel $idle_channel ]
}

proc start_game { track } {
  ::warninglog "Lightmaster -> $track"
  reset_state
  after 500 [ list ::Lightmaster::toggle_channel $track ]
}

proc toggle_channel { channel } {
  set command "/usr/local/bin/ola_set_dmx"
  set lightcommand [list exec $command "-u" "0" "-d" [channel_string $channel]]
  ::debuglog $lightcommand
  if [ catch $lightcommand err ] {
    ::warninglog "$err running $lightcommand"
  }
}

proc channel_string { channel } {
  set off 0
  set on 255
  set maxchannel 10
  for { set ch 1 } { $ch <= $maxchannel } { incr ch } {
    if { $ch == $channel } {
      append output $on ","
    } else {
      append output $off ","
    }
  }
  append output $off
  return $output
}

}
